/*
 * Copyright (C) 2023 Nuvoton Technology Corp. All rights reserved.
 *
 * SPDX-License-Identifier: BSD-3-Clause
 */

#include <arch.h>
#include <asm_macros.S>
#include <assert_macros.S>
#include <console_macros.S>
#include <drivers/nuvoton/uart_ma35d0.h>

	/*
	 * "core" functions are low-level implementations that don't require
	 * writable memory and are thus safe to call in BL1 crash context.
	 */
	.globl console_ma35d0_core_init
	.globl console_ma35d0_core_putc
	.globl console_ma35d0_core_getc
	.globl console_ma35d0_core_flush

	.globl console_ma35d0_putc
	.globl console_ma35d0_getc
	.globl console_ma35d0_flush

	/* -----------------------------------------------
	 * int console_ma35d0_core_init(uintptr_t base_addr,
	 * unsigned int uart_clk, unsigned int baud_rate)
	 * Function to initialize the console without a
	 * C Runtime to print debug information. This
	 * function will be accessed by console_init and
	 * crash reporting.
	 * In: x0 - console base address
	 *     w1 - Uart clock in Hz
	 *     w2 - Baud rate
	 * Out: return 1 on success, 0 on error
	 * Clobber list : x1, x2, x3
	 * -----------------------------------------------
	 */
func console_ma35d0_core_init
	/* Check the input base address */
	cbz	x0, init_fail
	/* Check baud rate and uart clock for sanity */
	cbz	w1, init_fail
	cbz	w2, init_fail

	/* Program the baudrate */
	/* Mode 2 */
	/* Divisor =  Uart clock / baudrate - 2 */
	udiv	w2, w1, w2
	sub w2, w2, #0x2
	and	w1, w2, #0xffff
	ldr	w3, [x0, #UARTBAUD]
	and w3, w3, #~0xff00ffff
	orr	w3, w3, #0x30000000
	orr	w3, w3, w1
	mov	w3, #0x00CE		// add by chp
	movk	w3, #0x3000, lsl #16		// add by chp
	str	w3, [x0, #UARTBAUD]

	/* 8n1 */
	mov	w3, #7
	str	w3, [x0, #UARTLCR]
	/* no interrupt */
	mov	w3, #0
	str	w3, [x0, #UARTIER]
	ret
init_fail:
	mov	w0, #0
	ret
endfunc console_ma35d0_core_init

	.globl console_ma35d0_register

	/* -----------------------------------------------
	 * int console_ma35d0_register(uintptr_t baseaddr,
	 *     uint32_t clock, uint32_t baud,
	 *     console_t *console);
	 * Function to initialize and register a new ma35d0
	 * console. Storage passed in for the console struct
	 * *must* be persistent (i.e. not from the stack).
	 * If w1 (UART clock) is 0, initialisation will be
	 * skipped, relying on previous code to have done
	 * this already. w2 is ignored then as well.
	 * In: x0 - UART register base address
	 *     w1 - UART clock in Hz
	 *     w2 - Baud rate (ignored if w1 is 0)
	 *     x3 - pointer to empty console_t struct
	 * Out: return 1 on success, 0 on error
	 * Clobber list : x0, x1, x2, x6, x7, x14
	 * -----------------------------------------------
	 */
func console_ma35d0_register
	mov	x7, x30
	mov	x6, x3
	cbz	x6, register_fail
	str	x0, [x6, #CONSOLE_T_BASE]

	/* A clock rate of zero means to skip the initialisation. */
	cbz	w1, register_ma35d0

	bl	console_ma35d0_core_init
	cbz	x0, register_fail

register_ma35d0:
	mov	x0, x6
	mov	x30, x7
	finish_console_register ma35d0 putc=1, getc=1, flush=1

register_fail:
	ret	x7
endfunc console_ma35d0_register

	/* --------------------------------------------------------
	 * int console_ma35d0_core_putc(int c, uintptr_t base_addr)
	 * Function to output a character over the console. It
	 * returns the character printed on success or -1 on error.
	 * In : w0 - character to be printed
	 *      x1 - console base address
	 * Out : return -1 on error else return character.
	 * Clobber list : x2
	 * --------------------------------------------------------
	 */
func console_ma35d0_core_putc
#if ENABLE_ASSERTIONS
	cmp	x1, #0
	ASM_ASSERT(ne)
#endif /* ENABLE_ASSERTIONS */

	/* Prepend '\r' to '\n' */
	cmp	w0, #0xA
	b.ne	2f
	/* Check if the transmit FIFO is full */
1:	ldr	w2, [x1, #UARTISR]
	and	w2, w2, #(UARTISR_THRE)
	cmp	w2, #(UARTISR_THRE)
	b.ne	1b
	mov	w2, #0xD		/* '\r' */
	str	w2, [x1, #UARTTX]

	/* Check if the transmit FIFO is full */
2:	ldr	w2, [x1, #UARTISR]
	and	w2, w2, #(UARTISR_THRE)
	cmp	w2, #(UARTISR_THRE)
	b.ne	2b
	str	w0, [x1, #UARTTX]
	ret
endfunc console_ma35d0_core_putc

	/* --------------------------------------------------------
	 * int console_ma35d0_putc(int c, console_t *console)
	 * Function to output a character over the console. It
	 * returns the character printed on success or -1 on error.
	 * In : w0 - character to be printed
	 *      x1 - pointer to console_t structure
	 * Out : return -1 on error else return character.
	 * Clobber list : x2
	 * --------------------------------------------------------
	 */
func console_ma35d0_putc
	ldr	x1, [x1, #CONSOLE_T_BASE]
	b	console_ma35d0_core_putc
endfunc console_ma35d0_putc

	/* ---------------------------------------------
	 * int console_ma35d0_core_getc(uintptr_t base_addr)
	 * Function to get a character from the console.
	 * It returns the character grabbed on success
	 * or -1 on if no character is available.
	 * In :  x0 - console base address
	 * Out : w0 - character if available, else -1
	 * Clobber list : x0, x1
	 * ---------------------------------------------
	 */
func console_ma35d0_core_getc

	/* Check if the receive FIFO is empty */
1:	ldr	w1, [x0, #UARTISR]
	tbz	w1, #UARTISR_RDR_BIT, no_char
	ldr	w0, [x0, #UARTRX]
	ret
no_char:
	mov	w0, #ERROR_NO_PENDING_CHAR
	ret
endfunc console_ma35d0_core_getc

	/* ---------------------------------------------
	 * int console_ma35d0_getc(console_t *console)
	 * Function to get a character from the console.
	 * It returns the character grabbed on success
	 * or -1 on if no character is available.
	 * In :  x0 - pointer to console_t stucture
	 * Out : w0 - character if available, else -1
	 * Clobber list : x0, x1
	 * ---------------------------------------------
	 */
func console_ma35d0_getc
	ldr	x0, [x0, #CONSOLE_T_BASE]
	b	console_ma35d0_core_getc
endfunc console_ma35d0_getc

	/* ---------------------------------------------
	 * int console_ma35d0_core_flush(uintptr_t base_addr)
	 * Function to force a write of all buffered
	 * data that hasn't been output.
	 * In : x0 - console base address
	 * Out : return -1 on error else return 0.
	 * Clobber list : x0, x1
	 * ---------------------------------------------
	 */
func console_ma35d0_core_flush

	/* Loop until the transmit FIFO is empty */
1:	ldr	w1, [x0, #UARTISR]
	and	w1, w1, #(UARTISR_THRE)
	cmp	w1, #(UARTISR_THRE)
	b.ne	1b

	mov	w0, #0
	ret
endfunc console_ma35d0_core_flush

	/* ---------------------------------------------
	 * int console_ma35d0_flush(console_t *console)
	 * Function to force a write of all buffered
	 * data that hasn't been output.
	 * In : x0 - pointer to console_t structure
	 * Out : return -1 on error else return 0.
	 * Clobber list : x0, x1
	 * ---------------------------------------------
	 */
func console_ma35d0_flush
	ldr	x0, [x0, #CONSOLE_T_BASE]
	b	console_ma35d0_core_flush
endfunc console_ma35d0_flush
